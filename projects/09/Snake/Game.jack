class Game {
  field Snake snake;
  field int direction;
  field int delay;
  field Grid grid;

  constructor Game new() {
    do Random.setSeed(RandomSeed.getSeed());
    let direction = 0;
    let grid = Grid.new(512, 256);
    let snake = Snake.new(grid.xSize() / 2, grid.ySize() / 2);

    do grid.placeFood();

    return this;
  }

  method void dispose() {
    do snake.dispose();
    do grid.dispose();
    do Memory.deAlloc(this);
    return;
  }

  method void run() {
    var char key;
    var boolean exit;

    while (~exit) {
      while (key = 0) {
        let key = Keyboard.keyPressed();
        do moveSnake();
      }

      if (key = 81) { let exit = true; }

      // up arrow
      if (key = 131 & ~(direction = 2)) {
        let direction = 1;
      }

      // down arrow
      if (key = 133 & ~(direction = 1)) {
        let direction = 2;
      }

      // left arrow
      if (key = 130 & ~(direction = 4)) {
        let direction = 3;
      }

      // right arrow
      if (key = 132 & ~(direction = 3)) {
        let direction = 4;
      }

      while (~(key = 0)) {
        let key = Keyboard.keyPressed();
        do moveSnake();
      }
    }
    return;
  }

  method void moveSnake() {
    do snake.move(direction);
    do Sys.wait(delay);
    return;
  }
}
